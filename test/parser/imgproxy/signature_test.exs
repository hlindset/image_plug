defmodule ImagePlug.Parser.Imgproxy.SignatureTest do
  use ExUnit.Case, async: true

  alias ImagePlug.Parser.Imgproxy
  alias ImagePlug.Parser.Imgproxy.Signature

  test "imgproxy parser exposes parser-owned option validation" do
    assert [signature: %Signature{mode: :disabled}] = Imgproxy.validate_options!([])
  end

  describe "validate_options!/1" do
    test "returns disabled config when imgproxy signature config is absent" do
      assert [signature: %Signature{mode: :disabled, key_salt_pairs: [], signature_size: 32}] =
               Signature.validate_options!([])
    end

    test "normalizes hex key and salt pairs" do
      assert [
               signature: %Signature{
                 mode: :enabled,
                 key_salt_pairs: [{"test-key", "test-salt"}],
                 signature_size: 8,
                 trusted_signatures: trusted_signatures
               }
             ] =
               Signature.validate_options!(
                 signature: [
                   keys: ["746573742d6b6579"],
                   salts: ["746573742d73616c74"],
                   signature_size: 8,
                   trusted_signatures: ["local-dev!"]
                 ]
               )

      assert MapSet.equal?(trusted_signatures, MapSet.new(["local-dev!"]))
    end

    test "rejects unknown signature options" do
      assert_raise ArgumentError, ~r/unknown options.*:trusted_signature/, fn ->
        Signature.validate_options!(
          signature: [
            keys: ["74657374"],
            salts: ["73616c74"],
            trusted_signature: ["typo"]
          ]
        )
      end
    end

    test "rejects unknown top-level imgproxy options" do
      assert_raise ArgumentError, ~r/unknown options.*:trusted_signatures/, fn ->
        Signature.validate_options!(trusted_signatures: ["local-dev!"])
      end

      assert_raise ArgumentError, ~r/unknown options.*:keys/, fn ->
        Signature.validate_options!(keys: ["74657374"], salts: ["73616c74"])
      end
    end

    test "rejects explicit nil signature config" do
      assert_raise ArgumentError, ~r/invalid value for :signature option/, fn ->
        Signature.validate_options!(signature: nil)
      end
    end

    test "rejects empty signing config" do
      assert_raise ArgumentError,
                   ~r/at least one key\/salt pair or trusted signature is required/,
                   fn ->
                     Signature.validate_options!(signature: [keys: [], salts: []])
                   end
    end

    test "supports trusted-only signing config" do
      assert [
               signature: %Signature{
                 mode: :enabled,
                 key_salt_pairs: [],
                 trusted_signatures: trusted_signatures
               }
             ] = Signature.validate_options!(signature: [trusted_signatures: ["local-dev!"]])

      assert MapSet.equal?(trusted_signatures, MapSet.new(["local-dev!"]))
    end

    test "rejects mismatched key and salt counts" do
      assert_raise ArgumentError, ~r/keys and salts must have the same length/, fn ->
        Signature.validate_options!(signature: [keys: ["74657374"], salts: []])
      end
    end

    test "rejects malformed hex key and salt values" do
      for config <- [
            [keys: ["not-hex"], salts: ["74657374"]],
            [keys: ["74657374"], salts: ["not-hex"]],
            [keys: [""], salts: ["74657374"]],
            [keys: ["74657374"], salts: [""]]
          ] do
        assert_raise ArgumentError,
                     ~r/keys and salts must be non-empty hex-encoded strings/,
                     fn ->
                       Signature.validate_options!(signature: config)
                     end
      end
    end

    test "rejects signature sizes outside 1..32" do
      for signature_size <- [0, 33] do
        assert_raise ArgumentError, ~r/signature_size must be an integer from 1 to 32/, fn ->
          Signature.validate_options!(
            signature: [
              keys: ["74657374"],
              salts: ["73616c74"],
              signature_size: signature_size
            ]
          )
        end
      end

      assert_raise ArgumentError, ~r/invalid value for :signature_size option/, fn ->
        Signature.validate_options!(
          signature: [
            keys: ["74657374"],
            salts: ["73616c74"],
            signature_size: "8"
          ]
        )
      end
    end

    test "rejects malformed trusted signatures" do
      for trusted_signatures <- ["local-dev", [""], [:not_binary]] do
        assert_raise ArgumentError,
                     ~r/trusted_signatures must be a list of non-empty strings/,
                     fn ->
                       Signature.validate_options!(
                         signature: [
                           keys: ["74657374"],
                           salts: ["73616c74"],
                           trusted_signatures: trusted_signatures
                         ]
                       )
                     end
      end
    end
  end

  describe "verify/3" do
    test "accepts disabled-signing placeholders when signing is disabled" do
      config = Signature.disabled()

      assert :ok = Signature.verify("_", "/plain/images/cat.jpg", config)
      assert :ok = Signature.verify("unsafe", "/plain/images/cat.jpg", config)

      assert Signature.verify("signed-value", "/plain/images/cat.jpg", config) ==
               {:error, {:unsupported_signature, "signed-value"}}
    end

    test "matches upstream full and truncated primitive HMAC vectors" do
      [signature: full] =
        Signature.validate_options!(
          signature: [
            keys: ["746573742d6b6579"],
            salts: ["746573742d73616c74"]
          ]
        )

      [signature: truncated] =
        Signature.validate_options!(
          signature: [
            keys: ["746573742d6b6579"],
            salts: ["746573742d73616c74"],
            signature_size: 8
          ]
        )

      assert :ok = Signature.verify("dtLwhdnPPiu_epMl1LrzheLpvHas-4mwvY6L3Z8WwlY", "asd", full)
      assert :ok = Signature.verify("dtLwhdnPPis", "asd", truncated)
      assert Signature.verify("dtLwhdnPPis", "asd", full) == {:error, :invalid_signature}
    end

    test "matches upstream key rotation vectors" do
      [signature: config] =
        Signature.validate_options!(
          signature: [
            keys: ["746573742d6b6579", "746573742d6b657932"],
            salts: ["746573742d73616c74", "746573742d73616c7432"]
          ]
        )

      assert :ok = Signature.verify("dtLwhdnPPiu_epMl1LrzheLpvHas-4mwvY6L3Z8WwlY", "asd", config)
      assert :ok = Signature.verify("jbDffNPt1-XBgDccsaE-XJB9lx8JIJqdeYIZKgOqZpg", "asd", config)
    end

    test "accepts exact trusted signatures before Base64 decoding" do
      [signature: config] =
        Signature.validate_options!(
          signature: [
            trusted_signatures: ["truested", "local-dev!"]
          ]
        )

      assert :ok = Signature.verify("truested", "asd", config)
      assert :ok = Signature.verify("local-dev!", "/w:300/plain/images/cat.jpg", config)

      assert Signature.verify("untrusted", "asd", config) ==
               {:error, {:invalid_signature_encoding, "untrusted"}}

      assert Signature.verify("aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa", "asd", config) ==
               {:error, :invalid_signature}
    end

    test "matches upstream processing handler request fixture" do
      [signature: config] =
        Signature.validate_options!(
          signature: [
            keys: ["746573742d6b6579"],
            salts: ["746573742d73616c74"]
          ]
        )

      signed_path = "/rs:fill:4:4/plain/local:///test1.png"

      assert :ok =
               Signature.verify(
                 "My9d3xq_PYpVHsPrCyww0Kh1w5KZeZhIlWhsa4az1TI",
                 signed_path,
                 config
               )

      assert Signature.verify("unsafe", signed_path, config) == {:error, :invalid_signature}
    end

    test "matches public docs signing vector" do
      [signature: config] =
        Signature.validate_options!(
          signature: [
            keys: ["736563726574"],
            salts: ["68656c6c6f"]
          ]
        )

      signed_path =
        "/rs:fill:300:400:0/g:sm/aHR0cDovL2V4YW1w/bGUuY29tL2ltYWdl/cy9jdXJpb3NpdHku/anBn.png"

      assert :ok =
               Signature.verify(
                 "oKfUtW34Dvo2BGQehJFR4Nr0_rIjOtdtzJ3QFsUcXH8",
                 signed_path,
                 config
               )
    end

    test "rejects malformed Base64 and wrong signatures" do
      [signature: config] =
        Signature.validate_options!(
          signature: [
            keys: ["746573742d6b6579"],
            salts: ["746573742d73616c74"]
          ]
        )

      assert Signature.verify("local-dev!", "/w:300/plain/images/cat.jpg", config) ==
               {:error, {:invalid_signature_encoding, "local-dev!"}}

      assert Signature.verify("dtLwhdnPPiu_epMl1LrzheLpvHas-4mwvY6L3Z8WwlY=", "asd", config) ==
               {:error,
                {:invalid_signature_encoding, "dtLwhdnPPiu_epMl1LrzheLpvHas-4mwvY6L3Z8WwlY="}}

      assert Signature.verify(
               "aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa",
               "/w:300/plain/images/cat.jpg",
               config
             ) == {:error, :invalid_signature}
    end

    test "rejects overlong encoded signatures before decoding" do
      [signature: config] =
        Signature.validate_options!(
          signature: [
            keys: ["746573742d6b6579"],
            salts: ["746573742d73616c74"]
          ]
        )

      overlong_signature = String.duplicate("a", 1_000)
      overlong_padded_signature = overlong_signature <> "="

      assert Signature.verify(overlong_signature, "/w:300/plain/images/cat.jpg", config) ==
               {:error, :invalid_signature}

      assert Signature.verify(overlong_padded_signature, "/w:300/plain/images/cat.jpg", config) ==
               {:error, :invalid_signature}
    end
  end
end
